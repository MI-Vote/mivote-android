// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    ext {
        compile_sdk = 33
        javaSourceVersion = JavaVersion.VERSION_1_8
        min_sdk = 21
        target_sdk = 33
        version_code = 1
        version_major = 0
        version_minor = 0
        version_patch = 1
        version_name = "$version_major.$version_minor.$version_patch"

        activity_compose_version = '1.6.0-alpha05'
        app_compat_version = '1.4.1'
        compose_version = '1.1.1'
        compose_navigation_version = "2.5.0-rc02"
        core_ktx_version = '1.7.0'
        detekt_version = '1.15.0-RC1'
        kotlin_version = '1.6.10'
        kotlin_coroutines_version = '1.5.1'
        hilt_version = "2.42"
        hilt_navigation_version = "1.0.0"
        lifecycle_version = '2.4.1'
        material_version = '1.5.0'
        mockk_version = "1.12.0"
        moshi_version = '1.13.0'
        okhttp_version = '4.9.0'
        retrofit_version = '2.9.0'
        room_version = "2.3.0"
        timber_version = "4.7.1"

        deps = [
            'androidX': [
                'compose': [
                    'activity': "androidx.activity:activity-compose:$activity_compose_version",
                    'material': "androidx.compose.material:material:$compose_version",
                    'navigation': "androidx.navigation:navigation-compose:$compose_navigation_version",
                    'runtimeLivedata': "androidx.compose.runtime:runtime-livedata:$compose_version",
                    'ui': "androidx.compose.ui:ui:$compose_version",
                    'uiTooling': "androidx.compose.ui:ui-tooling:1.0.0-beta09",
                ],
                'coreKtx': "androidx.core:core-ktx:$core_ktx_version",
                'appcompat': "androidx.appcompat:appcompat:$app_compat_version",
                'material': "com.google.android.material:material:$material_version"
            ],
            'desugar': 'com.android.tools:desugar_jdk_libs:1.1.5', // https://developer.android.com/studio/write/java8-support
            'hilt': [
                'core': "com.google.dagger:hilt-android:$hilt_version",
                'compiler': "com.google.dagger:hilt-compiler:$hilt_version",
                'navigation': "androidx.hilt:hilt-navigation-compose:$hilt_navigation_version"
            ],
            'lifecycle': [
                'livedataKtx': "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version",
                'runtimeKtx': "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version",
                'viewmodelKtx': "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version",
            ],
            'moshi': [
                'core' : "com.squareup.moshi:moshi:$moshi_version",
                'codeGen' : "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version",
                'adapters' : "com.squareup.moshi:moshi-adapters:$moshi_version"
            ],
            'okhttp': [
                'core': "com.squareup.okhttp3:okhttp:$okhttp_version",
                'logging': "com.squareup.okhttp3:logging-interceptor:$okhttp_version",
            ],
            'retrofit': [
                'core': "com.squareup.retrofit2:retrofit:$retrofit_version",
                'converter': "com.squareup.retrofit2:converter-scalars:$retrofit_version",
                'converterMoshi': "com.squareup.retrofit2:converter-moshi:$retrofit_version",
            ],
            'room': [
                'runtime': "androidx.room:room-runtime:$room_version",
                'ktx': "androidx.room:room-ktx:$room_version",
                'compiler': "androidx.room:room-compiler:$room_version",
            ],
            'timber': "com.jakewharton.timber:timber:$timber_version",
        ]
        testDeps = [
            'androidX': [
                'compose': [
                    'junit': "androidx.compose.ui:ui-test-junit4:$compose_version",
                    'testManifest': "androidx.compose.ui:ui-test-manifest:$compose_version",
                ],
                'espresso': 'androidx.test.espresso:espresso-core:3.4.0',
                'junit': "androidx.test.ext:junit:1.1.3",
                'lifecycle': 'androidx.arch.core:core-testing:2.1.0',
            ],
            'junit': 'junit:junit:4.13.2',
            'hilt': [
                'androidTesting': "com.google.dagger:hilt-android-testing:$hilt_version",
            ],
            'kotlinCoroutines': "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlin_coroutines_version",
            'mockk': [
                'core': "io.mockk:mockk:$mockk_version",
                'android': "io.mockk:mockk-android:$mockk_version",
            ],
        ]
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.42'

        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"

        classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0"
    }
}

apply plugin: "com.vanniktech.android.junit.jacoco"

junitJacoco {
    jacocoVersion = '0.8.7'
}

subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        toolVersion = detekt_version
        config = files("$rootProject.projectDir/detekt-config.yml")
        baseline = file("$rootProject.projectDir/detekt-baseline.xml")

        reports {
            html {
                enabled = true
                destination = file("$project.buildDir/reports/detekt.html")
            }
            xml {
                enabled = false
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            allWarningsAsErrors = true
            jvmTarget = javaSourceVersion
        }
    }
}


allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
